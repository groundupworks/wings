buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
    }
}

allprojects {
    repositories {
        jcenter()
        maven() {
            url "https://oss.sonatype.org/service/local/repositories/releases/content/"
        }
        mavenLocal()
    }
}

ext {
    supportV4Version = '25.3.1'
    if (LOCAL.toBoolean()) {
        RELEASE_REPOSITORY_URL = "file://${System.getProperty("user.home")}/.m2/repository"
        SNAPSHOT_REPOSITORY_URL = "file://${System.getProperty("user.home")}/.m2/repository/snapshots"
    }
}

configure(projects([':wings',
                    ':wings-dropbox',
                    ':wings-facebook',
                    ':wings-gcp'])) { project ->
    configureProject(project)
}

Set<Project> projects(Collection<String> paths) {
    def projects = []
    paths.each() { path -> projects << project(path) }
    return projects
}

void configureProject(Project project) {
    configure(project) {
        apply plugin: 'com.android.library'
        apply from: rootProject.file('gradle-mvn-push.gradle')

        group = GROUP
        version = VERSION_NAME

        android {
            buildToolsVersion '26.0.2'
            compileSdkVersion 26

            defaultConfig {
                minSdkVersion 15
                targetSdkVersion 26
                versionCode 0
                versionName VERSION_NAME
            }
            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                }
            }
        }
    }
}
