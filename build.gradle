buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.3'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenLocal()
    }
}

ext {
    supportV4Version = '22.0.0'
}

configure(projects([':wings',
                    ':wings-dropbox',
                    ':wings-facebook',
                    ':wings-gcp'])) { project ->
    configureProject(project)
}

Set<Project> projects(Collection<String> paths) {
    def projects = []
    paths.each() { path -> projects << project(path) }
    return projects
}

void configureProject(Project project) {
    configure(project) {
        apply plugin: 'com.android.library'
        apply plugin: 'com.github.dcendents.android-maven'
        apply from: 'https://raw.github.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'

        // Configurations for 'com.github.dcendents.android-maven'
        group = GROUP
        version = VERSION_NAME

        android {
            buildToolsVersion '22.0.1'
            compileSdkVersion 22

            defaultConfig {
                minSdkVersion 10
                targetSdkVersion 22
                versionCode 0
                versionName VERSION_NAME
            }
            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                }
            }
        }
    }
}
