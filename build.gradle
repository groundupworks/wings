buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0-rc1'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenLocal()
    }
}

ext {
    supportV4Version = '22.0.0'
}

configure(projects([':wings',
                    ':wings-dropbox',
                    ':wings-facebook',
                    ':wings-gcp'])) { project ->
    configureProject(project)
}

Set<Project> projects(Collection<String> paths) {
    def projects = []
    paths.each() { path -> projects << project(path) };
    return projects
}

void configureProject(Project project) {
    configure(project) {
        apply plugin: 'com.android.library'

        android {
            buildToolsVersion '22.0.0'
            compileSdkVersion 22

            defaultConfig {
                minSdkVersion 10
                targetSdkVersion 22
                versionCode 0
                versionName WINGS_VERSION
            }
            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                }
            }
        }
    }
}
