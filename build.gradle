buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.3'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenLocal()
    }
}

ext {
    supportV4Version = '22.0.0'
    if (LOCAL.toBoolean()) {
        RELEASE_REPOSITORY_URL = "file://${System.getProperty("user.home")}/.m2/repository"
        SNAPSHOT_REPOSITORY_URL = "file://${System.getProperty("user.home")}/.m2/repository/snapshots"
    }
}

configure(projects([':wings',
                    ':wings-dropbox',
                    ':wings-facebook',
                    ':wings-gcp'])) { project ->
    configureProject(project)
}

Set<Project> projects(Collection<String> paths) {
    def projects = []
    paths.each() { path -> projects << project(path) }
    return projects
}

void configureProject(Project project) {
    configure(project) {
        apply plugin: 'com.android.library'
        apply from: rootProject.file('gradle-mvn-push.gradle')

        group = GROUP
        version = VERSION_NAME

        android {
            buildToolsVersion '22.0.1'
            compileSdkVersion 22

            defaultConfig {
                minSdkVersion 10
                targetSdkVersion 22
                versionCode 0
                versionName VERSION_NAME
            }
            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                }
            }
        }

    }
}
